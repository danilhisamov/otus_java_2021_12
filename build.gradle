plugins {
    id 'idea'
    id 'java'
    id 'io.spring.dependency-management' version "${springDM_version}"
    id "org.springframework.boot" version "${springBoot_version}" apply false
    id 'com.github.johnrengelman.shadow' version "${shadow_version}" apply false
    id 'io.freefair.lombok' version "${lombok_version}" apply false
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.otus"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:${springBoot_version}")
                mavenBom("com.google.protobuf:protobuf-bom:${protobuf_version}")
            }

            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.glassfish:javax.json:${Versions.glassfishJson}")

            dependency("org.testcontainers:junit-jupiter:${Versions.testcontainers}")
            dependency("org.testcontainers:postgresql:${Versions.testcontainers}")

            dependency("io.grpc:grpc-netty:${Versions.grpc}")
            dependency("io.grpc:grpc-protobuf:${Versions.grpc}")
            dependency("io.grpc:grpc-stub:${Versions.grpc}")

            dependency("org.webjars:sockjs-client:${Versions.sockjs}")
            dependency("org.webjars:stomp-websocket:${Versions.stomp}")
            dependency("org.webjars:bootstrap:${Versions.bootstrap}")
            dependency("io.r2dbc:r2dbc-postgresql:${Versions.r2dbcPostgresql}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()

            force("net.java.dev.jna:jna:5.8.0")
            force("javax.servlet:servlet-api:2.4")
            force("commons-logging:commons-logging:1.1.1")
            force("commons-lang:commons-lang:2.5")
            force("org.codehaus.jackson:jackson-core-asl:1.8.8")
            force("org.codehaus.jackson:jackson-mapper-asl:1.8.3")
            force("org.codehaus.jettison:jettison:1.1")
            force("net.java.dev.jna:jna:5.8.0")
            force("com.google.errorprone:error_prone_annotations:2.7.1")
        }
    }
}
